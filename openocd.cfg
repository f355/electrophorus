proc read_file { name } {
	if {[catch {open $name r} fd]} {
		return ""
	}
	set result [read $fd]
	close $fd
	return $result
}

proc find_gpio_chip {} {
	# Prefer the RP1 GPIO chip on Pi 5; fall back to chip 0 (BCM) on Pi 4
	set matches [glob -nocomplain -directory "/proc/device-tree/aliases" "gpio\[0-9\]"]
	foreach f $matches {
		if {[string match "*/rp1/*" [read_file $f]]} {
			# Extract numeric suffix from alias filename: gpioN -> N
			return [string range [file tail $f] 4 end]
		}
	}
	return 0
}

set GPIO_CHIP [find_gpio_chip]

if {$GPIO_CHIP != 0} {
	# Raspberry Pi 5 (RP1 via linuxgpiod)
	adapter driver linuxgpiod
	adapter gpio swclk -chip $GPIO_CHIP 24
	adapter gpio swdio -chip $GPIO_CHIP 25
	adapter gpio srst -chip $GPIO_CHIP 23
} else {
	# Raspberry Pi 4 (BCM2835 GPIO)
	adapter driver bcm2835gpio
	bcm2835gpio speed_coeffs 450000 52
	adapter gpio swclk 24
	adapter gpio swdio 25
	adapter gpio srst 23
}

reset_config srst_only srst_push_pull

transport select swd
